plugins {
    id "net.ltgt.apt" version "0.20"
    id "com.diffplug.gradle.spotless" version "3.23.0"
    id "com.google.protobuf" version "0.8.8"
    id "java-library"
    id "maven-publish"
    id "idea"
}


dependencies {
    compile(project(":convert"))
    compile("org.apache.beam:beam-sdks-java-core:$beamVersion")
    compile("org.apache.beam:beam-runners-direct-java:$beamVersion")
    compile("org.apache.beam:beam-runners-google-cloud-dataflow-java:$beamVersion")
    compile("org.apache.beam:beam-sdks-java-extensions-protobuf:$beamVersion")
    compile("org.apache.beam:beam-sdks-java-extensions-google-cloud-platform-core:$beamVersion")
    compile("org.apache.beam:beam-sdks-java-io-google-cloud-platform:$beamVersion")
    compile("org.apache.beam:beam-sdks-java-io-common:$beamVersion")
    testImplementation("org.apache.beam:beam-sdks-java-extensions-sql:$beamVersion")
    testImplementation("junit:junit:4.12")
    testImplementation("org.hamcrest:hamcrest-all:1.3")
    testProtobuf(project(":proto-examples"))
    
    testCompile("org.apache.beam:beam-runners-direct-java:$beamVersion")
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protobufVersion"
    }
}

spotless {
    java {
        googleJavaFormat('1.7')
        target project.fileTree(project.projectDir) {
            include "**/*.java"
            exclude "build/**/*.java"
        }
    }
}

publishing {
    repositories {
        maven {
            // change to point to your repo, e.g. http://my.org/repo
            if(project.version.endsWith('-SNAPSHOT')) {
                url = uri(snapshotRepoUrl)
            } else {
                url = uri(repoUrl)
            }
            if (project.hasProperty('repoUser') && project.hasProperty('repoPass')) {
                credentials {
                    username repoUser
                    password repoPass
                }
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId 'io.anemos'
            artifactId 'protobeam-transform'
            from components.java
        }
    }
}