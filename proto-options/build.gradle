plugins {
    id "net.ltgt.apt" version "0.20"
    id "com.google.protobuf" version "0.8.8"
    id 'java-library'
    id 'maven-publish'
    id 'idea'
}

dependencies {
    compile("com.google.protobuf:protobuf-java:$protobufVersion")
    implementation("com.google.protobuf:protobuf-java:$protobufVersion")
    implementation("com.google.protobuf:protobuf-java-util:$protobufVersion")
    testImplementation("junit:junit:4.12")
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protobufVersion"
    }
}

task protoJaxr(type: Jar) {
    from('src/main/proto') {
        include '/**/*'
    }
}

project.afterEvaluate {
    publishing {
        repositories {
            maven {
                // change to point to your repo, e.g. http://my.org/repo
                if (project.version.endsWith('-SNAPSHOT')) {
                    url = uri(snapshotRepoUrl)
                } else {
                    url = uri(repoUrl)
                }
                if (project.hasProperty('repoUser') && project.hasProperty('repoPass')) {
                    credentials {
                        username repoUser
                        password repoPass
                    }
                }
            }
        }
        publications {
            maven(MavenPublication) {
                groupId 'io.anemos'
                artifactId 'protobeam-options-proto'

                artifact protoJaxr
            }
        }
    }
}
